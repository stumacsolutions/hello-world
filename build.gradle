buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10')
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE')
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'checkstyle'
apply plugin: 'docker'
apply plugin: 'findbugs'
apply plugin: 'idea'
apply plugin: 'info.solidsoft.pitest'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'spring-boot'

group = 'hello'
version = 'latest'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    testCompile('info.cukes:cucumber-java:1.2.4')
    testCompile('info.cukes:cucumber-junit:1.2.4')
    testCompile('info.cukes:cucumber-spring:1.2.4')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

jar {
    baseName = 'hello-world'
    version = 'latest'
}

pitest {
    coverageThreshold = 50 // increase threshold when there is more code
    mutationThreshold = 90
}

test {
    exclude 'acceptance/**'
}

task cucumber(type: Test) {
    include 'acceptance/**'
}

task docker(type: Docker, dependsOn: assemble) {
  push = true
  applicationName = jar.baseName
  dockerfile = file('Dockerfile')
  doFirst {
    copy {
      from jar
      into stageDir
    }
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
